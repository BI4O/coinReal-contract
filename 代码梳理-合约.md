# CoinReal的代码梳理

## 整体架构

CoinReal采用模块化架构，核心由以下合约组成：

- **App.sol**: 核心入口合约，整合所有子系统
- **UserManager.sol**: 用户管理系统
- **TopicManager.sol**: 话题和活动管理系统
- **ActionManager.sol**: 评论和点赞动作管理系统
- **Campaign.sol**: 活动代币合约（ERC20）
- **CampaignFactory.sol**: 活动代币工厂合约
- **TimeSerLinkArray.sol**: 时间序列链表，用于用户活动历史
- **CountSerLinkArray.sol**: 点赞数排序链表，用于评论排行

## 用户管理模块UserManager

### User结构

| 字段名       | 类型      | 说明         |
| ------------ | --------- | ------------ |
| id         | uint    | 用户id       |
| name       | string  | 用户名称     |
| bio        | string  | 用户简介     |
| email      | string  | 用户邮箱     |
| registered | bool    | 是否已注册   |

### 增

##### um.registerUser(user, name, bio, email) 

只有user、name、bio、email是必须的

- id自动递增
- register自动为true

```solidity
// 增：用户注册
function registerUser(
    address _user,
    string memory _name, 
    string memory _bio, 
    string memory _email
) public {
    require(!users[_user].registered, "User already registered");
    require(bytes(_name).length > 0, "Name is required");
    users[_user] = User({
        id: nextUserId,
        name: _name,
        bio: _bio,
        email: _email,
        registered: true
    });
    usersById[nextUserId] = _user;
    nextUserId++;
}
```

### 查

##### um.getUserInfo(address _user) => User

```solidity
function getUserInfo(address _user) public view returns (User memory) {
    return users[_user];
}
```

##### um.getUserInfoById(uint _id) => User

```solidity
function getUserInfoById(uint _id) public view returns (User memory) {
    return users[usersById[_id]];
}
```

### 改

##### um.updateUser(user,name,bio,email)

```solidity
function updateUser(
    address _user,
    string memory _name, 
    string memory _bio, 
    string memory _email
) public {
    require(users[_user].registered, "User not registered");
    if (bytes(_name).length > 0) {
        users[_user].name = _name;
    }
    if (bytes(_bio).length > 0) {
        users[_user].bio = _bio;
    }
    if (bytes(_email).length > 0) {
        users[_user].email = _email;
    }
}
```

### 删

##### um.deleteUser(address _user)

```solidity
function deleteUser(address _user) public {
    delete users[_user];
    delete usersById[users[_user].id];
}
```

## 话题管理模块（基础）

### Topic表

| 字段名       | 类型    | 描述          |
| ----------- | ------- | ------------- |
| id           | uint    | 项目id        |
| name | string  | 项目名称      |
| description | string  | 项目描述      |
| tokenAddress | string  | 项目token地址 |
| tokenPrice   | uint    | 项目token价格 |
| dataFeed     | address | 数据源地址    |
| commentCount | uint    | 评论数        |
| likeCount    | uint    | 点赞数        |

### 增

##### tm.registerTopic(name, description, tokenAddress, tokenPrice)

只有name、desc、tokenAddr、tokenPrice是个必须

- id自动
- dataFeed后续绑定
- commentCount外部联动才增加的
- likeCount外部联动才增加的

```solidity
function registerTopic(
    string memory _name, 
    string memory _description, 
    string memory _tokenAddress,
    uint _tokenPrice
) public {
    topics[nextTopicId] = Topic({
        id: nextTopicId,
        name: _name,
        description: _description,
        tokenAddress: _tokenAddress,
        tokenPrice: _tokenPrice,  // 初始化价格
        dataFeed: address(0), // 默认数据源地址为0
        commentCount: 0,
        likeCount: 0
    });
    // 话题名称不能为空
    require(bytes(topics[nextTopicId].name).length > 0, "Topic name is empty");
    topicsByTokenAddress[_tokenAddress] = topics[nextTopicId];
    nextTopicId++;
}
```

### 查

##### tm.getTopic(uint _topicId) => Topic

```solidity
// 查：根据id获取话题
function getTopic(uint _topicId) public view returns (Topic memory) {
    return topics[_topicId];
}
```

##### tm.getTopicByTokenAddress(tokenAddress) => Topic

```solidity
// 查：根据tokenAddress获取话题
function getTopicByTokenAddress(string memory _tokenAddress) public view returns (Topic memory) {
    return topicsByTokenAddress[_tokenAddress];
}
```

##### tm.listTopics() => Topic[]

```solidity
// 查：列出所有话题
function listTopics() public view returns (Topic[] memory) {
    Topic[] memory topicsList = new Topic[](nextTopicId);
    for (uint i = 0; i < nextTopicId; i++) {
        topicsList[i] = topics[i];
    }
    return topicsList;
}
```

### 改

目前只能修改价格，或者删掉重新创建

##### tm.updateTopicTokenPrice(uint _topicId, uint _tokenPrice)

```solidity
// 改：手动更新话题的token价格
function updateTopicTokenPrice(uint _topicId, uint _tokenPrice) public {
    // 如果已经删除了，则name为空，不允许更新价格
    require(bytes(topics[_topicId].name).length > 0, "Topic not exist");
    topics[_topicId].tokenPrice = _tokenPrice;
}

// 改：自动更新话题的token价格 TODO chainlink-datafeed
function updateTopicTokenPriceAuto(uint _topicId) public {
    // 如果已经删除了，则name为空，不允许更新价格
    require(bytes(topics[_topicId].name).length > 0, "Topic not exist");

    // chainlink-datafeed
    // 数据源地址不为0
    require(topics[_topicId].dataFeed != address(0), "Data feed not set");
    
    // TODO 后续改成用chainlink-datafeed获取价格
    topics[_topicId].tokenPrice = 0;
}
```

### 删

##### tm.deleteTopicToken(uint _topicId)

```solidity
// 删：删除话题
function deleteTopicToken(uint _topicId) public {
    delete topicsByTokenAddress[topics[_topicId].tokenAddress];
    delete topics[_topicId];        
}
```

## 话题管理模块（高级）

### CampaignInfo表

| 字段名                        | 数据类型 | 描述                                |
| ----------------------------- | -------- | ----------------------------------- |
| id                            | uint     | 活动id                              |
| **sponsor**                   | address  | 活动赞助商                          |
| **topicId**                   | uint     | 话题id：外键                        |
| **name**                      | string   | 活动名称                            |
| **description**               | string   | 活动描述                            |
| **projectTokenAddr**          | address  | 项目代币地址                        |
| isActive                      | bool     | 活动是否激活                        |
| startTime                     | uint     | 活动开始时间                        |
| endTime                       | uint     | 活动结束时间                        |
| jackpot                       | uint     | 奖金池(美元计价)                    |
| mintTokenAmount               | uint     | 活动期间mint的token数量(decimal 18) |
| rewardUsdcPerMillionCPToken   | uint     | 每百万CP token的得USDC数            |
| rewardPtokenPerMillionCPToken | uint     | 每百万CP token的奖励                |

### 绑定说明

CampaignInfo，是通过topicId绑定在某个topic下面的，一个topic最多有3个进行中的campaignInfo

> [!important]
>
> 如果已知一个topicId，可以找到他关联的campaignInfo
>
> ##### topicCampaigns[topicId] => [campaignInfo1, campaignInfo2...]
>
> 通过这些找到的cid = campaignInfo.id，可以找到对应的campaignToken地址
>
> ##### campaigns[cid] => campaignTokenAddress

### 增

##### tm.registerCampaign(sponsor, topicId, name, description, projectTokenAddr) => (true, campaignId)

```solidity
function registerCampaign(
    address _sponsor,
    uint _topicId,
    string memory _name,
    string memory _description,
    address _projectTokenAddr
) public returns (bool, uint) {
    // 检查topic是否存在
    require(bytes(topics[_topicId].name).length > 0, "Topic not exist");

    // 检查该topic的campaign数量限制
    require(topicCampaigns[_topicId].length < MAX_CAMPAIGNS_PER_TOPIC, "Topic campaign limit reached");
    
    // 通过factory创建campaign token
    address campaignToken = campaignFactory.createCampaignToken(
        _name,
        string(abi.encodePacked(_name, "Token")),
        _topicId,
        _projectTokenAddr
    );
    
    // 设置campaign token地址 
    campaigns[nextCampaignId] = campaignToken;
    // 根据topicId，将campaignId添加到topicCampaigns中
    topicCampaigns[_topicId].push(nextCampaignId);
    
    // 设置campaign信息，注意此时活动还没有start，所以isActive为false
    campaignInfos[nextCampaignId] = CampaignInfo({
        // 创建参数
        id: nextCampaignId,                     // 活动id
        sponsor: _sponsor,                      // 活动赞助商
        topicId: _topicId,                      // 话题id
        name: _name,                            // 活动名称
        description: _description,              // 活动描述
        projectTokenAddr: _projectTokenAddr,    // 项目代币地址

        // 非创建参数
        isActive: false,                        // 开始后转true，结束后再转false，活动是否激活
        startTime: 0,                           // 开始后记录：活动开始时间
        endTime: 0,                             // 开始后记录：活动结束时间
        jackpot: 0,                             // 开始后记录：活动奖金池
        mintTokenAmount: 0,                     // 结束后记录：活动期间mint的token数量
        rewardUsdcPerMillionCPToken: 0,         // 结束后记录：每百万CP token的得多少颗USDC
        rewardPtokenPerMillionCPToken: 0        // 结束后记录：每百万CP token的得多少颗项目方token
    });

    nextCampaignId++;

    // 返回创建成功和创建的campaignId
    return (true, nextCampaignId - 1);
}
```

> [!note]
>
> 增加完成之后，是一个ERC20的CampaignToken，需要通过活动注资和启动
>
> ```solidity
> // 活动开始前注资USDC
> function fundCampaignWithUSDC(uint _campaignId, uint _amount) public {
>     CampaignToken c = CampaignToken(campaigns[_campaignId]);
>     usdc.transferFrom(msg.sender, address(c), _amount);
> }
> 
> // 活动开始前注资项目代币
> function fundCampaignWithProjectToken(uint _campaignId, uint _amount) public {
>     CampaignToken c = CampaignToken(campaigns[_campaignId]);
>     c.projectToken().transferFrom(msg.sender, address(c), _amount);
> }   
> ```
>
> 然后才可以开始活动

### 查

##### tm.getTopicCampaigns(topicId) => [campaignId1, campaignId2...]

根据topicId查下面有哪几个campaignign，用campaignId表示

```solidity
function getTopicCampaigns(uint _topicId) public view returns (uint[] memory) {
    return topicCampaigns[_topicId];
}
```

##### tm.getCampaignInfo(campaignId) => campaignInfo

再根据这些campaignId找到对应的camapignInfo

```solidity
function getCampaignInfo(uint _campaignId) public view returns (CampaignInfo memory) {
    return campaignInfos[_campaignId];
}
```

### 改：启动

##### tm.startCampaign(uint _campaignId, uint _endTime)

> [!note]
>
> 但是这个函数运行之前要用check来检查
>
> ##### tm.checkCampaignCanStart(uint _campaignId) public view returns (bool)
>
> 如果不通过，说明还没有注资，可以用以下方式先注资
>
> ##### function fundCampaignWithProjectToken(uint _campaignId, uint _amount) 
>
> ##### function fundCampaignWithUSDC(uint _campaignId, uint _amount)

check通过之后，才可以开始这个活动

```solidity
// 改：活动开始
function startCampaign(uint _campaignId, uint _endTime) public {
    require(_campaignId < nextCampaignId, "Campaign does not exist");
    require(checkCampaignCanStart(_campaignId) == true, "Campaign funding is not enough");
    CampaignToken c = CampaignToken(campaigns[_campaignId]);

    // token的信息更新到campaignInfo中
    c.start(_endTime);
    // 更新四项信息
    campaignInfos[_campaignId].isActive = c.isActive();
    campaignInfos[_campaignId].startTime = c.startTime();
    campaignInfos[_campaignId].endTime = c.endTime();
    campaignInfos[_campaignId].jackpot = c.jackpotStart(); // 这里只是初始值，但是会浮动

    // 检查token的信息是否更新到campaignInfo中，如果没更新，则说明start失败
    require(campaignInfos[_campaignId].isActive == true, "Campaign start isActive didn't change");
}
```

### 改：结束

##### tm.endCampaign(uint _campaignId)

```solidity
// 活动结束
function endCampaign(uint _campaignId) public {
    require(_campaignId < nextCampaignId, "Campaign does not exist");
    require(campaignInfos[_campaignId].isActive, "Campaign is not active");

    CampaignToken c = CampaignToken(campaigns[_campaignId]);

    // 尝试调用finish方法，如果失败，则尝试调用finishByLowTotalJackpot方法
    bool isFinished = c.finish();
    if (!isFinished) {
        isFinished = c.finishByLowTotalJackpot();
    }
    require(isFinished, "Campaign finish failed");

    // 更新结构体信息
    campaignInfos[_campaignId].isActive = c.isActive();
    campaignInfos[_campaignId].mintTokenAmount = c.totalSupply();
    campaignInfos[_campaignId].rewardUsdcPerMillionCPToken = c.usdcPerMillionCPToken();
    campaignInfos[_campaignId].rewardPtokenPerMillionCPToken = c.ptokenPerMillionCPToken();
    campaignInfos[_campaignId].jackpot = c.jackpotRealTime();
}
```

### 删

##### tm.deleteTopic(uint _topicId)

```solidity
// 删除活动
function deleteTopic(uint _topicId) public {
    require(_topicId < nextTopicId, "Topic does not exist");

    // 删除topic
    deleteTopicToken(_topicId);

    // 找到这些关联的campaignId
    uint[] memory campaignIds = topicCampaigns[_topicId];
    for (uint i = 0; i < campaignIds.length; i++) {
        // 删除campaign
        delete campaigns[campaignIds[i]];
        delete campaignInfos[campaignIds[i]];
    }
    // 删除topicCampaigns中的campaignId
    topicCampaigns[_topicId] = new uint[](0);       
}
```

## 动作管理模块

### Comment表

| 字段名     | 类型      | 描述         |
| ---------- | --------- | ------------ |
| id         | uint      | 评论id       |
| user       | address   | 用户地址     |
| topicId    | uint      | 话题id       |
| timestamp  | uint      | 时间戳       |
| likeCount  | uint      | 点赞数       |
| isDelete   | bool      | 是否删除     |
| content    | string    | 评论内容     |
| tags       | string[]  | 评论标签     |

### Like表

| 字段名     | 类型      | 描述         |
| ---------- | --------- | ------------ |
| id         | uint      | 点赞id       |
| user       | address   | 用户地址     |
| topicId    | uint      | 话题id       |
| timestamp  | uint      | 时间戳       |
| commentId  | uint      | 评论id       |
| isCancel   | bool      | 是否取消     |

### 增：评论

##### am.addComment(topicId, user, content) => (true, commentId)

```solidity
function addComment(
    uint _topicId, 
    address _user, 
    string memory _content
) public returns(bool,uint){
    // topic必须存在
    require(bytes(topicManager.getTopic(_topicId).name).length > 0, "Topic does not exist");
    // user必须存在
    require(userManager.getUserInfo(_user).registered, "User does not exist");

    // 创建评论
    comments[nextCommentId] = Comment({
        id: nextCommentId,
        topicId: _topicId,
        user: _user,
        content: _content,
        likeCount: 0,
        timestamp: block.timestamp,
        isDelete: false,
        tags: new string[](0)
    });

    // 奖励代币
    commentRewardOrBurn(_topicId, nextCommentId, _user, false);

    // 更新topic的commentCount
    nextCommentId++;

    return (true, nextCommentId - 1);
}
```

### 增：点赞

##### am.addLike(topicId, commentId, user) => (true, likeId)

```solidity
function addLike(
    uint _topicId, 
    uint _commentId,
    address _user
) public returns(bool,uint){
    // 校验用户是否已经注册
    require(userManager.getUserInfo(_user).registered, "User does not exist");

    // 校验topic是否存在
    require(bytes(topicManager.getTopic(_topicId).name).length > 0, "Topic does not exist");

    // comment必须存在
    require(comments[_commentId].isDelete != true, "Comment does not exist");

    // 防止重复点赞
    require(hasLiked[_commentId][_user] != true, "You have already liked this comment");

    // 创建点赞
    likes[nextLikeId] = Like({
        id: nextLikeId,
        topicId: _topicId,
        user: _user,
        commentId: _commentId,
        timestamp: block.timestamp,
        isCancel: false
    });

    // 更新hasLiked
    hasLiked[_commentId][_user] = true;

    // 更新comment的likeCount
    comments[_commentId].likeCount++;

    // 奖励代币
    likeReward(_topicId, nextLikeId, _user);

    // 更新nextLikeId
    nextLikeId++;

    return (true, nextLikeId - 1);
}
```

### 查：评论

##### am.getComment(uint _commentId) => Comment

```solidity
function getComment(uint _commentId) public view returns (Comment memory) {
    return comments[_commentId];
}
```

### 查：点赞

##### am.getLike(uint _likeId) => Like

```solidity
function getLike(uint _likeId) public view returns (Like memory) {
    return likes[_likeId];
}
```

### 查：评论点赞数

##### am.getLikeCount(uint _commentId) => uint

```solidity
function getLikeCount(uint _commentId) public view returns (uint) {
    return comments[_commentId].likeCount;
}
```

### 查：评论标签

##### am.getCommentTags(uint _commentId) => string[]

```solidity
function getCommentTags(uint _commentId) public view returns (string[] memory) {
    return comments[_commentId].tags;
}
```

### 改：添加标签

##### am.addTags(uint commentId, string[] memory tags)

```solidity
function addTags(uint commentId, string[] memory tags) public {
    // 评论必须存在 - 修改检查逻辑
    require(commentId < nextCommentId, "Comment does not exist");
    // 评论必须未删除
    require(comments[commentId].isDelete != true, "Comment is already deleted");
    // 增加tags
    comments[commentId].tags = tags;
}
```

### 删：删除评论

##### am.deleteComment(uint _commentId) => bool

```solidity
function deleteComment(uint _commentId) public returns(bool) {
    // 只有评论作者可以删除
    require(comments[_commentId].user == msg.sender, "You are not the author of this comment");
    // 评论必须未删除
    require(comments[_commentId].isDelete != true, "Comment is already deleted");  

    // 取消之前评论获得的代币
    commentRewardOrBurn(
        comments[_commentId].topicId, 
        _commentId, 
        comments[_commentId].user,
        true
    );

    // 删除评论
    comments[_commentId].isDelete = true;

    return true;
}
```

##### am.deleteCommentByUser(uint _commentId, address _user) => bool

```solidity
function deleteCommentByUser(uint _commentId, address _user) public returns(bool) {
    // 只有评论作者可以删除
    require(comments[_commentId].user == _user, "You are not the author of this comment");
    // 评论必须未删除
    require(comments[_commentId].isDelete != true, "Comment is already deleted");  

    // 取消之前评论获得的代币
    commentRewardOrBurn(
        comments[_commentId].topicId, 
        _commentId, 
        comments[_commentId].user,
        true
    );

    // 删除评论
    comments[_commentId].isDelete = true;

    return true;
}
```

## 新增查询功能和高级特性

### 用户活动查询

##### am.getRecentCommentsByUserAddress(address _userAddress, uint _n) => uint[]

获取用户最近的n个评论ID（按时间倒序）

```solidity
function getRecentCommentsByUserAddress(address _userAddress, uint _n) public view returns (uint[] memory) {
    return timeSerLinkArray.getRecentItems(_userAddress, timeSerLinkArray.COMMENT_LIST(), _n);
}
```

##### am.getRecentLikesByUserAddress(address _userAddress, uint _n) => uint[]

获取用户最近的n个点赞ID（按时间倒序）

```solidity
function getRecentLikesByUserAddress(address _userAddress, uint _n) public view returns (uint[] memory) {
    return timeSerLinkArray.getRecentItems(_userAddress, timeSerLinkArray.LIKE_LIST(), _n);
}
```

### 评论排行查询

##### am.getMostLikedComments(uint _n) => uint[]

获取点赞数最多的n个评论ID

```solidity
function getMostLikedComments(uint _n) public view returns (uint[] memory) {
    return countSerLinkArray.getMostLikedComments(_n);
}
```

##### am.getLeastLikedComments(uint _n) => uint[]

获取点赞数最少的n个评论ID

```solidity
function getLeastLikedComments(uint _n) public view returns (uint[] memory) {
    return countSerLinkArray.getLeastLikedComments(_n);
}
```

### 分页查询功能

##### am.getMostLikedCommentsPaginated(uint startIndex, uint length) => uint[]

分页获取最多点赞的评论ID

```solidity
function getMostLikedCommentsPaginated(uint startIndex, uint length) public view returns (uint[] memory) {
    return countSerLinkArray.getMostLikedCommentsPaginated(startIndex, length);
}
```

##### am.getRecentCommentsPaginated(uint startIndex, uint length) => uint[]

分页获取最近评论ID（全局时间序列）

```solidity
function getRecentCommentsPaginated(uint startIndex, uint length) public view returns (uint[] memory) {
    return timeSerLinkArray.getRecentItemsPaginated(timeSerLinkArray.COMMENT_LIST(), startIndex, length);
}
```

##### am.getValidCommentsCount() => uint

获取有效评论总数（用于分页计算）

```solidity
function getValidCommentsCount() public view returns (uint) {
    return countSerLinkArray.getValidCommentsCount();
}
```

##### am.getGlobalCommentsCount() => uint

获取全局评论总数（用于时间序列分页计算）

```solidity
function getGlobalCommentsCount() public view returns (uint) {
    return timeSerLinkArray.getGlobalListSize(timeSerLinkArray.COMMENT_LIST());
}
```

## 高级数据结构实现

### TimeSerLinkArray - 时间序列链表

用于维护用户活动历史和全局活动历史，按时间倒序排列。

**特点**:
- O(1) 时间复杂度插入新项目到头部
- 支持用户个人历史和全局历史的双重维护
- 高效的分页查询支持

### CountSerLinkArray - 点赞数排序链表

用于维护评论按点赞数的全局排序，支持动态更新。

**特点**:
- 有序插入，避免全量重建和排序
- 支持评论删除和恢复
- 从两端遍历：头部最多点赞，尾部最少点赞
- 自动跳过已删除的评论

## Chainlink集成功能

### 1. AI情感分析标签系统

##### addCommentAITag(uint commentId) => bool

为评论添加AI情感分析标签，使用Chainlink Functions调用外部AI服务。

##### getCommentAITag(uint commentId) => string

获取评论的AI情感标签（POS/NEG/NEU）。

### 2. VRF随机数抽奖系统

##### performCampaignLottery(uint campaignId) => bool

执行活动结束时的随机抽奖，为点赞者分配奖励。

##### checkCampaignLotteryNeeded(uint campaignId) => bool

检查活动是否需要执行抽奖。

### 3. 自动化批处理

##### performBatchCommentTagging(uint[] commentIds) => uint

批量为评论添加AI标签，支持Chainlink Automation。

## 奖励系统

### 费用分配机制

活动注资时的费用分配：
- **平台费**: 归平台所有
- **质量评论者费**: 分配给最多点赞的评论作者
- **抽奖费**: 用于点赞者随机抽奖
- **剩余资金**: 进入活动奖池，按CP token比例分配

### 预期奖励查询

##### getExpectedReward(uint campaignId, address user) => (uint[2], uint[2])

返回用户的预期奖励范围：
- worstCase: 最差情况奖励 [USDC, ProjectToken]
- bestCase: 最好情况奖励 [USDC, ProjectToken]

##### getFundPoolInfo(uint campaignId) => (uint[3], uint[3], uint[3])

返回活动奖池信息：
- qualityPool: 质量评论者奖池 [人数, USDC, ProjectToken]
- lotteryPool: 点赞抽奖奖池 [人数, USDC, ProjectToken]  
- campaignPool: CampaignToken奖池 [总参与者, USDC, ProjectToken]
        